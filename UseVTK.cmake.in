#
# This module add the VTK include paths to a project
# It should be included after the FindVTK module
#

# This file must support both the CMake 1.4 FindVTK.cmake module and
# the new VTK_DIR based find.  If any of the old variables are set,
# the VTKConfig.cmake file has not yet been included.  Otherwise, this
# file is being included from the VTK_USE_FILE setting provided in
# VTKConfig.cmake, so the settings have already been loaded.

# If VTK_USE_FILE is set, then VTKConfig.cmake has already been loaded.
IF(NOT VTK_USE_FILE)
  # We are being included through USE_VTK_FILE.  Load the settings.
  IF(USE_INSTALLED_VTK)
    INCLUDE(${VTK_INSTALL_PATH}/lib/vtk/VTKConfig.cmake)
  ELSE(USE_INSTALLED_VTK)
    IF(USE_BUILT_VTK)
      INCLUDE(${VTK_BINARY_PATH}/VTKConfig.cmake)
    ENDIF(USE_BUILT_VTK)
  ENDIF(USE_INSTALLED_VTK)
ENDIF(NOT VTK_USE_FILE)

# Now we have the settings from VTKConfig.cmake.
INCLUDE_DIRECTORIES(${VTK_INCLUDE_DIRS})
LINK_DIRECTORIES(${VTK_LIBRARY_DIRS})
ADD_DEFINITIONS(${VTK_DEFINITIONS})

# Make sure the compiler settings match those used to build VTK.
IF(NOT VTK_NO_ENFORCE_CXX_COMPILER)
  IF(VTK_CXX_COMPILER_MISMATCH)
    MESSAGE(SEND_ERROR
            "The selected VTK was built with "
            "\"${VTK_CXX_COMPILER}\""
            ", but the current compiler is \"${CMAKE_CXX_COMPILER}\".  "
            "Please set CMAKE_CXX_COMPILER to \"${VTK_CXX_COMPILER}\".")
  ENDIF(VTK_CXX_COMPILER_MISMATCH)
  IF(NOT VTK_NO_ENFORCE_BUILD_TYPE)
    # Build type only needs to match on windows.  A user project can
    # always set VTK_NO_ENFORCE_BUILD_TYPE and then check the build type
    # itself to get the check on UNIX and Windows.
    IF(WIN32)
      IF(VTK_BUILD_TYPE_MISMATCH)
        MESSAGE(SEND_ERROR
                "The selected VTK was built with build type "
                "\"${VTK_BUILD_TYPE}\""
                ", but the current build type is \"${CMAKE_BUILD_TYPE}\".  "
                "Please set CMAKE_BUILD_TYPE to \"${VTK_BUILD_TYPE}\".")
      ENDIF(VTK_BUILD_TYPE_MISMATCH)
    ENDIF(WIN32)
  ENDIF(NOT VTK_NO_ENFORCE_BUILD_TYPE)
ENDIF(NOT VTK_NO_ENFORCE_CXX_COMPILER)
