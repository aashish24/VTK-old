#-----------------------------------------------------------------------------
# Discover the name of the runtime library path environment variable,
# which varies from platform to platform.  Since it depends on the compiler
# options (32-bit vs. 64-bit) on some platforms, a TRY_RUN is needed.

# The result is put in SHARED_LIBRARY_PATH_VAR_NAME

TRY_RUN(SHARED_LIBRARY_PATH_TYPE SHARED_LIBRARY_PATH_INFO_COMPILED
        ${PROJECT_BINARY_DIR}/CMakeTmp
        ${PROJECT_SOURCE_DIR}/CMake/SharedLibraryPathInfo.cxx
        OUTPUT_VARIABLE OUTPUT
        ARGS "LDPATH")

# for CMAKE 2.2 and later, get path from vtkSharedLibraryPathInfo's stdout
IF("${CMAKE_MAJOR_VERSION}.${CMAKE_MINOR_VERSION}" GREATER 2.0)  
  STRING(REGEX MATCH "([a-zA-Z][a-zA-Z0-9_]*)(\r|\n)*$" OUTPUT "${OUTPUT}")
  STRING(REGEX MATCH "[a-zA-Z][a-zA-Z0-9_]*" OUTPUT "${OUTPUT}")
  SET(SHARED_LIBRARY_PATH_VAR_NAME "${OUTPUT}")
ELSE("${CMAKE_MAJOR_VERSION}.${CMAKE_MINOR_VERSION}" GREATER 2.0)
  # this code is to support CMake 2.0 and earlier
  SET(SHARED_LIBRARY_PATH_VAR_NAME "LD_LIBRARY_PATH")
  IF (CMAKE_SYSTEM_NAME MATCHES "Darwin")
    SET(SHARED_LIBRARY_PATH_VAR_NAME "DYLD_LIBRARY_PATH")
  ENDIF (CMAKE_SYSTEM_NAME MATCHES "Darwin")
  IF (CMAKE_SYSTEM_NAME MATCHES "SunOS.*")
    IF (SHARED_LIBRARY_PATH_TYPE EQUAL 64)
      SET(SHARED_LIBRARY_PATH_VAR_NAME "LD_LIBRARY_PATH_64")
    ENDIF (SHARED_LIBRARY_PATH_TYPE EQUAL 64)
  ENDIF (CMAKE_SYSTEM_NAME MATCHES "SunOS.*")
  IF (CMAKE_SYSTEM_NAME MATCHES "IRIX.*")
    IF (SHARED_LIBRARY_PATH_TYPE EQUAL 32)
      SET(SHARED_LIBRARY_PATH_VAR_NAME "LD_LIBRARYN32_PATH")
    ENDIF (SHARED_LIBRARY_PATH_TYPE EQUAL 32)
    IF (SHARED_LIBRARY_PATH_TYPE EQUAL 64)
      SET(SHARED_LIBRARY_PATH_VAR_NAME "LD_LIBRARY64_PATH")
    ENDIF (SHARED_LIBRARY_PATH_TYPE EQUAL 64)
  ENDIF (CMAKE_SYSTEM_NAME MATCHES "IRIX.*")
  IF (CMAKE_SYSTEM_NAME MATCHES "AIX")
    SET(SHARED_LIBRARY_PATH_VAR_NAME "LIBPATH")
  ENDIF (CMAKE_SYSTEM_NAME MATCHES "AIX")
  IF (CMAKE_SYSTEM_NAME MATCHES "HP-UX")
    IF (SHARED_LIBRARY_PATH_TYPE LESS 64)
      SET(SHARED_LIBRARY_PATH_VAR_NAME "SHLIB_PATH")
    ENDIF (SHARED_LIBRARY_PATH_TYPE LESS 64)
  ENDIF (CMAKE_SYSTEM_NAME MATCHES "HP-UX")
  IF (WIN32)
    SET(SHARED_LIBRARY_PATH_VAR_NAME "PATH")
  ENDIF (WIN32)
ENDIF("${CMAKE_MAJOR_VERSION}.${CMAKE_MINOR_VERSION}" GREATER 2.0)    

SET(SHARED_LIBRARY_PATH_VAR_NAME ${SHARED_LIBRARY_PATH_VAR_NAME} CACHE INTERNAL "runtime library path variable name.")

